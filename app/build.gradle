plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.samentic.youtubeclient'
    compileSdk 32

    defaultConfig {
        applicationId "com.samentic.youtubeclient"
        minSdk 24
        //noinspection OldTargetApi
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def props = new Properties()
        File secretFile = rootProject.file("client_secret.properties")
        if(secretFile.exists()) {
            secretFile.withInputStream { props.load(it)}
        }

        String clientId = props.getProperty("ID", "")
        String redirectScheme = clientId.tokenize(".").reverse().join(".")
        manifestPlaceholders += [redirectUriScheme: redirectScheme]
        buildConfigField("String", "CLIENT_ID", "\"${clientId}\"")
        buildConfigField("String", "CLIENT_SECRET", "\"${props.getProperty("SECRET", "")}\"")
        buildConfigField("String", "API_KEY", "\"${props.getProperty("KEY", "")}\"")
        buildConfigField("String", "REDIRECT_URL_SCHEME", "\"${redirectScheme}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    // this is added because of google-api related dependencies
    packagingOptions {
        excludes += 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'

    // UI
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.Zhuinden:fragmentviewbindingdelegate-kt:1.0.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // loading image
    implementation("io.coil-kt:coil:2.2.2")
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // Dagger
    implementation 'com.google.dagger:dagger:2.44.2'
    kapt 'com.google.dagger:dagger-compiler:2.44.2'

    // OAuth2
    implementation 'net.openid:appauth:0.11.1'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'

    implementation("com.google.api-client:google-api-client-gson:2.1.1")
    implementation("com.google.api-client:google-api-client:2.1.1")
    implementation("com.google.apis:google-api-services-youtube:v3-rev20221108-2.0.0")

    // Room
    implementation 'androidx.room:room-runtime:2.4.3'
    kapt 'androidx.room:room-compiler:2.4.3'
    implementation 'androidx.room:room-ktx:2.4.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}